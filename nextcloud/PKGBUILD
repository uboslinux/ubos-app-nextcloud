developer=https://nextcloud.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=22.1.1
pkgrel=1
pkgdesc="A safe home for all your data"
arch=('any')
license=("AGPL3")
source=("https://download.nextcloud.com/server/releases/nextcloud-${pkgver}.tar.bz2")
releasepage=('https://nextcloud.com/changelog/')
depends=(
    'php'
    'php-smbclient'
    'php-systemd'
    'smtp-server')
options=('!strip')
sha512sums=('0b418d7d0e0e825ec9259334e377449a8dcd183dc907b094361c70b51b00732353996fdbf12f17c06cfa6bb918221cac56ee3051df0308ff453bb2d727e9c0ad')

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Systemd
    install -D -m0644 ${startdir}/systemd/nextcloud@.timer -t ${pkgdir}/etc/systemd/system/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code
    install -D -m0755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

    cp -dr --no-preserve=ownership ${startdir}/src/nextcloud ${pkgdir}/ubos/share/${pkgname}/
}
