developer=https://nextcloud.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=26.0.11
pkgrel=1
pkgdesc="A safe home for all your data"
arch=('any')
license=("AGPL3")
source=("https://download.nextcloud.com/server/releases/nextcloud-${pkgver}.tar.bz2")
releasepage=('https://nextcloud.com/changelog/')
depends=(
    'php'
    'php-smbclient'
    'php-systemd'
    'smtp-server')
options=('!strip')
sha512sums=('2e4c3d19e014814266e830d3a03387effb327e761d47e3a1dcb2b065b7c4b7da89c2a329997f986c771d8f45442cebc976e1607d1048b90266d3bf349b4261f7')

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Systemd
    install -D -m0644 ${startdir}/systemd/nextcloud@.timer -t ${pkgdir}/etc/systemd/system/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code
    install -D -m0755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

    cp -dr --no-preserve=ownership ${startdir}/src/nextcloud ${pkgdir}/ubos/share/${pkgname}/
}
