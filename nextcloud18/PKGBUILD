developer=https://nextcloud.com/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=18.0.12
pkgrel=1
pkgdesc="A safe home for all your data"
arch=('any')
license=("AGPL3")
source=("https://download.nextcloud.com/server/releases/nextcloud-${pkgver}.tar.bz2")
releasepage=('https://nextcloud.com/changelog/')
depends=(
    'php'
    'php-systemd'
    'smtp-server')
options=('!strip')
sha512sums=('cd9679a730768d143f9cabda8c5b29782369bed7310d31a408358a8b5f0e9794a7ce802e1fdfbb78fbc936160fa681be874d252c4d5e3f985951ad8c2fd48f81')

package() {
# Manifest
    install -D -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/ubos/lib/ubos/manifests/${pkgname}.json

# Icons
    install -D -m0644 ${startdir}/appicons/{72x72,144x144}.png -t ${pkgdir}/ubos/http/_appicons/${pkgname}/

# Systemd
    install -D -m0644 ${startdir}/systemd/nextcloud18@.timer -t ${pkgdir}/etc/systemd/system/

# Data
    mkdir -p ${pkgdir}/ubos/lib/${pkgname}

# Code
    install -D -m0755 ${startdir}/bin/*.pl -t ${pkgdir}/ubos/share/${pkgname}/bin/

    install -D -m0644 ${startdir}/tmpl/*.tmpl -t ${pkgdir}/ubos/share/${pkgname}/tmpl/

    cp -dr --no-preserve=ownership ${startdir}/src/nextcloud ${pkgdir}/ubos/share/${pkgname}/
}
